#region Prolog

# #----------------------------------------------------
#  Define constant
# #----------------------------------------------------

cProcessName = GetProcessName();

sErr = '' ;

cTimeStamp = TIMST( NOW() , '\Y\m\d\h\i\s' ) | NumberToString( RAND * 1000000000 );
cTempObjName = Expand('%cProcessName%_%cTimeStamp%');
cSourceObj='source_'|cTempObjName;
cTargetRandObjName = 'Target'|cProcessName|cTimeStamp;
cTemp = 1 ;

#--------------
If(pVersionFrom@=pVersionTo & pPeriodFrom@=pPeriodTo);
    sErr='Source and target cannot be the same!';
    ProcessBreak; 
Endif;    

# # ----------------------------------------------------
# # zero target view
# # ----------------------------------------------------

cTargetCube = 'plan_Profit_and_Loss';

ViewCreate( cTargetCube , cTargetRandObjName , cTemp );

SubsetCreatebyMDX( cTargetRandObjName , '{[plan_Version].[plan_Version].['|pVersionTo |']}' , cTemp ); 
SubsetCreatebyMDX( cTargetRandObjName , '{[Currency].[Currency].[LCY]}' , cTemp );
SubsetCreatebyMDX( cTargetRandObjName , '{[plan_Period].[plan_Period].['|pPeriodTo |']}' , cTemp ); 
SubsetCreatebyMDX( cTargetRandObjName , '{TM1FILTERBYLEVEL(TM1SUBSETALL([Branch].[Branch]) , 0)}' , cTemp );
SubsetCreatebyMDX( cTargetRandObjName , '{TM1FILTERBYLEVEL(TM1SUBSETALL([Business_Unit].[Business_Unit]) , 0)}' , cTemp );
SubsetCreatebyMDX( cTargetRandObjName , '{TM1FILTERBYLEVEL(TM1SUBSETALL([Account_PL].[Account_PL]) , 0)}', 1 );
SubsetCreatebyMDX( cTargetRandObjName , '{[m_plan_Profit_and_Loss].[m_plan_Profit_and_Loss].[Value]}',cTemp );

nDimCount = 1;
WHILE( TABDIM( cTargetCube , nDimCount )@<> '' );
	sDim = TABDIM( cTargetCube , nDimCount );
	IF( SUBSETEXISTS( sDim , cTargetRandObjName ) = 1 );
		VIEWSUBSETASSIGN( cTargetCube , cTargetRandObjName , sDim , cTargetRandObjName );
	ENDIF;
	nDimCount = nDimCount +1;
END;

#----------------------------------------------------
ViewSuppressZeroesSet( cTargetCube , cTargetRandObjName , 1 );
ViewExtractSkipZeroesSet(cTargetCube, cTargetRandObjName , 1 );
ViewExtractSkipRuleValuesSet(cTargetCube, cTargetRandObjName, 1);


#---Zero Out the view in the Target Version 
ViewZeroOut(cTargetCube, cTargetRandObjName);


#-----------------
# Set source view
#-----------------
ViewCreate( 'plan_Profit_and_Loss' , cSourceObj , 1 );

SubsetCreatebyMDX( cSourceObj , '{[plan_Version].[plan_Version].['|pVersionFrom |']}' , cTemp ); 
SubsetCreatebyMDX( cSourceObj , '{[Currency].[Currency].[LCY]}' , cTemp );
SubsetCreatebyMDX( cSourceObj , '{[plan_Period].[plan_Period].['|pPeriodFrom |']}' , cTemp ); 
SubsetCreatebyMDX( cSourceObj , '{TM1FILTERBYLEVEL(TM1SUBSETALL([Branch].[Branch]) , 0)}' , cTemp );
SubsetCreatebyMDX( cSourceObj , '{TM1FILTERBYLEVEL(TM1SUBSETALL([Business_Unit].[Business_Unit]) , 0)}' , cTemp );
SubsetCreatebyMDX( cSourceObj , '{TM1FILTERBYLEVEL(TM1SUBSETALL([Account_PL].[Account_PL]) , 0)}', 1 );
SubsetCreatebyMDX( cSourceObj , '{[m_plan_Profit_and_Loss].[m_plan_Profit_and_Loss].[Value]}',cTemp );

nDimCount = 1;
WHILE( TABDIM( 'plan_Profit_and_Loss' , nDimCount )@<> '' );
	sDim = TABDIM( 'plan_Profit_and_Loss' , nDimCount );
	IF( SUBSETEXISTS( sDim , cSourceObj ) = 1 );
		VIEWSUBSETASSIGN( 'plan_Profit_and_Loss' , cSourceObj , sDim , cSourceObj );
	ENDIF;
	nDimCount = nDimCount +1;
END;

ViewSuppressZeroesSet( 'plan_Profit_and_Loss' , cSourceObj , 1 );
ViewExtractSkipCalcsSet( 'plan_Profit_and_Loss' , cSourceObj , 1 );
ViewExtractSkipZeroesSet( 'plan_Profit_and_Loss' , cSourceObj , 1 );
If(pVersionType@='Calculated');
    ViewExtractSkipRuleValuesSet('plan_Profit_and_Loss', cSourceObj, 1);
Else;
    ViewExtractSkipRuleValuesSet('plan_Profit_and_Loss', cSourceObj, 0);
Endif;
 

DataSourceType = 'VIEW' ;
DataSourceNameForServer = 'plan_Profit_and_Loss' ;
DataSourceCubeView = cSourceObj ;

#endregion
#region Data

if(CellIsUpdateable('plan_Profit_and_Loss',pVersionTo,vCurrency,pPeriodTo, vBranch, vBusiness_Unit, vAccount_PL, vm_plan_Profit_and_Loss )=1);
CellPutN( vValue, 'plan_Profit_and_Loss',pVersionTo,vCurrency,pPeriodTo, vBranch, vBusiness_Unit, vAccount_PL, vm_plan_Profit_and_Loss );
endif;

#endregion
#region Epilog

If(sErr@<>'');
    ItemReject( sErr );
Endif;    
#endregion